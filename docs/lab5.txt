Lab 5
Still, Owens


Despcription of covered phenomena

1. Nominal Phrase Inflections


The construction of a nominal phrase in Dagaare is comprised of an optional determiner, followed by the noun stem, followed by optional adjectives, followed by the inflection for number. This inflection must attach to only the final noun or adjective in the phrase. This means that when adjecives exist in the phrase, the noun stem is left in its root form. We have not found any constraints on relative adjective ordering, other than always occurring to the right of the noun stem. The data provided to us accounts for concatenation of up to 5 adjectives.

Source: author
Vetted: f
Judgment: g
Phenomena: adjectives
a badɛr bile kpie la
a   badɛr  bil-e    kpi-e         la
DEF spider small-SG die-PERF.INTR FOC
'The small spider dies'


Our initial solution built a morphological rule which created a plural form from any adjective.
While our grammar was able to parse the above example, it over-generated when given an inflected noun followed by an adjective. Following the example and direction from Dr. Bender, we were able to more accurately model this phenomena by implementing a cascade of edits. Following Dr. Bender's lead we made the following changes:

We added the feature NUMBER-FLAG to the INFLECTED feature, and required that NUMBER-FLAG be N/A or + for a lexeme to be infl-satisfied:

inflected :+ [ ASPECT-FLAG luk,
               NUMBER-FLAG luk ].

infl-satisfied :+ [ ASPECT-FLAG na-or-+,
                    NUMBER-FLAG na-or-+ ].

We then defined nouns and adjectives to by default have INFLECTED.NUMBER-FLAG -, so that they would have to pass through lexical rules to be infl-satisfied:

cn-lex-noun-lex := noun-lex &
  [ SYNSEM.LOCAL [ CAT.HEAD.PRON -,
                   CONT.HOOK.INDEX.PNG.PER 3rd ],
    INFLECTED.NUMBER-FLAG - ].

adj-lex := basic-intersective-adjective-lex &
  [ INFLECTED.NUMBER-FLAG - ].

The supertype for human nouns was defined to mark NUMBER-FLAG as +, allowing the lexeme to be inf-satisfied:

num-hmn-lex-rule-super := add-only-no-ccont-rule &
  [ INFLECTED.NUMBER-FLAG +,
    DTR hmn-cn-lx-noun-lex ].

One instantiation of this was the num-hmn-uninfl-lex-rule, which specifies that the noun must not be on the right periphery:

num-hmn-uninfl-lex-rule := num-hmn-lex-rule-super & constant-lex-rule &
  [ SYNSEM.R-PERIPH - ].

The other option is that the noun goes through an inflecting rule - either singular or plural - which both inherit from marked-num-hmn-lex-rule, which specifies that the noun must be on the right periphery:

marked-num-hmn-lex-rule := infl-lex-rule & num-hmn-lex-rule-super &
  [ SYNSEM.R-PERIPH + ].

num-hmn-sg-lex-rule := marked-num-hmn-lex-rule &
  [ SYNSEM.LOCAL.CONT.HOOK.INDEX.PNG.NUM sg ].

num-hmn-pl-lex-rule := marked-num-hmn-lex-rule &
  [ SYNSEM.LOCAL.CONT.HOOK.INDEX.PNG.NUM pl ].


The set of rules above have been reduplicated for each noun class and a separate set modified to be relevant for adjectives.
This coverage ensures that the nominal phrase inflection pattern is consistent and accurate.

The phrase rules modified are as follows:


head-adj-int-phrase :+ [ SYNSEM.R-PERIPH #rpr,
                         HEAD-DTR.SYNSEM.R-PERIPH -,
                         NON-HEAD-DTR.SYNSEM [ R-PERIPH #rpr,
                                               LOCAL.CAT [ HEAD.POSSESSOR nonpossessive,
                                                           POSSESSUM nonpossessive ] ] ].

head-adj-scop-phrase :+ [ NON-HEAD-DTR.SYNSEM.LOCAL.CAT [ HEAD.POSSESSOR nonpossessive,
                                    POSSESSUM nonpossessive ] ].

The above rule takes an adjective as a non head daughter and copies up the R-PERIPH value to the head daughter.


head-spec-phrase := basic-head-spec-phrase & head-final &
  [ HEAD-DTR.SYNSEM.R-PERIPH +,
    NON-HEAD-DTR.SYNSEM [ OPT -,
                          LOCAL.CAT [ VAL.SPR < >,
                                      HEAD +vjrcdmo &
                                           [ POSSESSOR nonpossessive ] ] ] ].

The above rule ensures that the head daughter has been marked as R-PERIPH, and is thus already inflected.



2. Pronomial Possession


Pronouns were grouped into two main categories, nom-pron-lex and acc-pron-lex, corresponding to the two main cases. nom-pron-lex is further divided into str-pron-lex and wk-pron-lex, which represent to the "strong" and "weak" distinction. As most "weak" forms are identical to their accusative counterparts, we intend to mark such pronouns as unspecified for case; however, we were not able to effect this change for this lab due to time constraints. We assume that, despite the vague case marking, Dagaare dissallows accusative pronouns from acting as possessors. We continue to wait on a response from Dr. Grimm regarding this.


Source: author
Vetted: f
Judgment: g
Phenomena: possessives
n badɛre kpie la
n     badɛr-V   kpi-e         la
1s.WK spider-SG die-PERF.INTR FOC
'My spider dies'


Source: author
Vetted: f
Judgment: u
Phenomena: possessives
*ma badɛre kpie la
ma     badɛr-V   kpi-e         la
1s.ACC spider-SG die-PERF.INTR FOC
'Me's spider dies'


To constrain the case of possessors, we specified that poss-1-phrase only accept a non-head daughter with the case value nom. We also discovered that we needed to allow the poss-phrase-1 to accept pronouns as the non-head daughter input.


poss-phrase-1 := head-final &
  [ SYNSEM.LOCAL.CAT [ HEAD #head,
                       VAL [ COMPS < >,
                             SUBJ < >,
                             SPR < > ] ],
    HEAD-DTR.SYNSEM.LOCAL [ CAT [ HEAD #head & noun &
                                       [ PRON - ],
                                  VAL.SPR < [ ] > ],
                            CONT.HOOK #hook &
                                      [ LTOP #lbl,
                                        INDEX #possessum &
                                              [ COG-ST uniq-id ] ] ],
    C-CONT [ HOOK #hook &
                  [ INDEX #possessum ],
             ICONS <!  !>,
             RELS <! arg12-ev-relation &
                     [ PRED "poss_rel",
                       LBL #lbl,
                       ARG1 #possessum,
                       ARG2 #possessor ], quant-relation &
                                          [ PRED "exist_q_rel",
                                            ARG0 #possessum,
                                            RSTR #harg ] !>,
             HCONS <! qeq &
                      [ HARG #harg,
                        LARG #lbl ] !> ],
    NON-HEAD-DTR.SYNSEM.LOCAL [ CONT.HOOK.INDEX #possessor,
                                CAT [ VAL.SPR < >,
                                      HEAD noun &
                                           [ CASE nom ] ] ] ].




3. Adverbial modifiers


Dagaare's adverbs primarily occur at the end of the phrase they are modifying. The information structure marker, "la", is currently analyzed as an adverb, an analysis we inherited from the matrix customization system. This focal marker was creating ambiguity by attaching to the subject of focus using two separate rules, scopal and intersective.

	# Adapted from a:98
	Source: author
	Vetted: f
	Judgment: g
	Phenomena: adverbs
	a koɔraa zengɛɛ la liglig
	a    kɔr-V     zeŋ-e         la   liglig
	DEF  farmer-sg sit-PERF.INTR FOC quietly
	'The farmer sat down quietly'


Dealing with the initial scopal/intersective distinction reduced our overall ambiguity by a significant amount. The following fix was implemented by Dr. Bender:

focus-marking-mod-lex := infostr-marking-mod-lex &
  [ SYNSEM.LOCAL.CAT [ MKG fc,
                       HEAD.MOD < [ L-PERIPH luk,
                                    LOCAL intersective-mod &
                                          [ CAT.HEAD +nv,
                                            CONT.HOOK.ICONS-KEY focus ] ] > ] ].


In order to begin our modeling of the adverbial system, we introduced the provided lexical rule (below) to the dagaare.tdl file.

adverb-lex := basic-adverb-lex & intersective-mod-lex &
  [ SYNSEM [ LOCAL [ CAT [ HEAD.MOD < [ LOCAL.CAT.HEAD verb ]>,
         VAL [ SPR < >,
         SUBJ < >,
         COMPS < >,
         SPEC < > ]]]]].

An adverb lexical entry allowed us to parse sentences and ensure that the MRS relfected the expected behavior.

liglig :=  adverb-lex &
  [ STEM < "liglig" >,
    SYNSEM.LKEYS.KEYREL.PRED "_quickly_a_rel " ].




4. Definiteness

4.1. Determiners and Bare NPs

Dagaare has only one clearly identifiable determiner. The determiner "a" is definite, contributing both the cognative statuses of familiarity and activeness. The determiner is optional; a bare noun is considered indefinite.


Source: author
Vetted: f
Judgment: g
Phenomena: determiners
koɔraa kpie la
kɔr-V     kpi-e         la
farmer-SG die-PERF.INTR FOC
'a farmer dies'


Source: author
Vetted: f
Judgment: g
Phenomena: determiners
a koɔraa kpie la
a   kɔr-V     kpi-e         la
DEF farmer-SG die-PERF.INTR FOC
'the farmer dies'


To imbue these constructions with the correct cognative status, we changed dagaare.tdl so the COG-ST value of determiners would reflect the contributions discussed above. In addition, we marked the COG-ST value of bare-nouns as indefinite.

def-det-lx-determiner-lex := determiner-lex & no-icons-lex-item &
  [ SYNSEM.LOCAL.CAT.VAL.SPEC.FIRST.LOCAL.CONT.HOOK.INDEX.COG-ST activ+fam ].

bare-np-phrase := basic-bare-np-phrase &
  [ SYNSEM.LOCAL [ CONT.HOOK.INDEX.COG-ST type-id,
                   CAT.VAL.SPEC < > ],
    C-CONT.RELS <! [ PRED "exist_q_rel" ] !> ].


The change to the bare-np-phrase had the latent effect of barring any pronoun from passing through the bare-np-phrase rule. This was remedied by introducing a new rule type to the dagaare.tdl file and corresponding type to the rule.tdl file. The new rule specified that the head daughter of the resulting phrase had to be a pronoun.


bare-pron-np-phrase := basic-bare-np-phrase &
  [ SYNSEM.LOCAL.CAT.VAL.SPEC < > ,
    HEAD-DTR.SYNSEM.LOCAL.CAT.HEAD.PRON + ,
    C-CONT.RELS <! [ PRED "exist_q_rel" ] !> ].


bare-pron-np := bare-pron-np-phrase.


This required a further edit to the dagaare.tdl file:

cn-lex-noun-lex := noun-lex &
  [ SYNSEM.LOCAL [ CAT.HEAD.PRON -,
                   CONT.HOOK.INDEX.PNG.PER 3rd ] ].

By declaring that all common noun lexemes are explicitly PRON -,  we sought to constrain the bare-pron-np-phrase to only allow pronouns through. However, after making these changes we have seen instances of ambiguity occuring with coordinated phrases. Our hypothesis is that the coordinator does not copy up the PRON value from its daughters, leaving thus leaving the pronominal feature underspecified. This "bare-np" can be seen when attempting to parse the following example:

Source: author
Vetted: f
Judgment: g
Phenomena: coordination
kɔribɔ ane badɛr ane bie gaa la sakuuri
kɔr-V     ane  badɛr-V  ane  bi-V     gaa     la  saku-ri
farmer-SG CONJ badɛr-SG CONJ child-SG go.PERF FOC school-SG
'a farmer and a spider and a child have gone to school'


4.2. Pronouns

As noted above, pronouns exhibit a strong and weak distinction. This distinction is irrelevant when considering the COG-ST a pronoun carries. Whether nominative or accusative, Dagaare's pronomial system conveys specificity, as well the prerequisit knowledge of whom is being referred to. This information was encoded in the following addition to the INDEX feature structure of pron-lex-noun-lex in the dagaare.tdl file:

pron-lex-noun-lex := no-spr-noun-lex &
  [ SYNSEM.LOCAL.CONT.HOOK.INDEX [ COG-ST activ-or-more,
                                   SPECI + ] ].


5. Current Coverage

From lab 4 to lab 5 we saw a massive reduction in ambiguity. This is largly thanks to the resolution of the scopal/intersective ambiguity, as well as our refining of the pronominal possession issue. In addition we saw a rise in successful, correct parses involving number marking of the nominal phrase. In all our overgeneration analysis resulted in 46.9% due to unresolved ambiguity. Our coverage sits at 67.1%. In comparison with out lab 4 results, our overall average analyses count dropped from 3.35 to 1.68.


6. Unresolved issues

We have unwittingly allowed NPs to be identified as coordinated phrases, despite the absence of any coordinand. This is exceedingly suprising, and we do not have strong intuition as to how to fix this bug. The following is an example where this type of coordinand occurs:

Source: b:38
Vetted: s
Judgment: g
Phenomena: valchange
a boore nyu la a koɔ
a   bʊɔ-ri  nyu   la  a   kʊɔ
DEF goat-PL drink FOC DEF water
'The goats have drunk the water'

It seems that we have a case of stacking verbs. While the number of parses has dropped from 7 to 1, the example below should not parse at all. We discovered this directly before submission, so we will need to address this in our next lab.

Source: author
Vetted: f
Judgment: u
Phenomena: possessives
*ba ba ba kpie
ba         ba         ba         kpi-e
3pl.HMN.WK 3pl.HMN.WK NONFUT.NEG die-PERF.INTR
'Their they do not die'
